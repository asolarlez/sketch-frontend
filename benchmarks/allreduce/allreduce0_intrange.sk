pragma options "-V10 --bnd-int-range 8 --beopt:simiters 100 --fe-trunc-var-arr --spmd-max-nproc 4 --bnd-inbits 3 --bnd-cbits 2 --bnd-unroll-amnt 4 --bnd-inline-amnt 5 --bnd-arr1d-size 4 --bnd-arr-size 4 --be:ufunSymmetry --beopt:simplifycex NOSIM"; //  --be:showDAG

package spmd;
// NOTE: how to refer to spmd::some_var if I am not in spmd package?

int SPMD_MAX_NPROC = 4;
int SPMD_MAX_BUF_LEN = 2;
include "spmd.skh";

include "generators2.skh";

struct LState {
	int n;
	double[n] a;
}

struct LSingle {
	double val;
}

LState movein(global int ncols, global double[ncols] ga) {
	assert ncols % spmdnproc == 0;
	int n = ncols / spmdnproc;
	int base = n * spmdpid;
	double[n] a;
	for (int i=0; i<n; i++) {
		a[i] = ga[base+i];
	}
	LState s = new LState(n=n, a=a);
	return s;
}

void moveout(global int ncols, ref global double[ncols] ga, LState s) {
	int n = s.n;
	int base = n * spmdpid;
	for (int i=0; i<n; i++) {
		ga[base+i] = s.a[i];
	}
}

LSingle movein_single(global double[spmdnproc] gv) {
	LSingle s = new LSingle(val=gv[spmdpid]);
	return s;
}

void moveout_single(ref global double[spmdnproc] gv, LSingle s) {
	gv[spmdpid] = s.val;
}

bit powerOf2(int n) {
	if (n > 0) {
		while (n > 1) {
			if (n % 2 != 0) {
				return false;
			}
			n = n / 2;
		}
		return true;
	} else {
		return false;
	}
}

// NOTE: how to assert that value is on every processor? now the double[P] result seems clumsy!
void spec_rnorm(global int P, global int ncols, global double[ncols] r, ref global double[P] result) {
	assume powerOf2(P);
	assume ncols>0 && ncols%P == 0;
	assume P <= SPMD_MAX_NPROC;
	assume ncols <= 4;
	double sum = 0.0;
	for (int i=0; i<ncols; i++) {
		sum = sum + r[i] * r[i];
	}
	for (int p=0; p<P; p++) {
		result[p] = sum;
	}
}

int setup_l2npcols() {
	global int n = spmdnproc/2;
	global int l2npcols = 0;
	while (n > 0) {
		l2npcols++;
		n = n /2;
	}
	return l2npcols;
}

void setup_reduce(int l2npcols, ref int[l2npcols] arr) {
	int div_factor = spmdnproc;
	for (int i=0; i<l2npcols; i++) {
		//int j = (spmdpid + div_factor/2) % div_factor + (spmdpid/div_factor)*div_factor;
		int ops_usage;
		int j = expr2(3, {spmdpid, div_factor, div_factor/2}, 4, {PLUS, TIMES, DIV, MOD}, ops_usage);
		arr[i] = j;
		div_factor = div_factor/2;
	}
}

double allreduce(global int l2npcols, int[l2npcols] reduce_exch_proc, double x) {
	double[1] sum;
	sum[0] = x;
	double[1] buf;
	for (global int i=0; i<l2npcols; i++) {
		int recipient = reduce_exch_proc[i];
		//spmdtransfer(global int size, bit scond, double [size] sendbuf, int recipient, bit rcond, ref double [size] recvbuf)
		spmdtransfer(1, 1, sum, recipient, 1, buf);
		sum[0] = sum[0] + buf[0];
	}
	return sum[0];
}

void sk_rnorm(int P, int ncols, double[ncols] r, ref global double[P] result) implements spec_rnorm {
	spmdfork(P) {
		LState s = movein(ncols, r);
		LSingle res = movein_single(result);

		global int l2npcols = setup_l2npcols();
		int[l2npcols] reduce_exch_proc;
		setup_reduce(l2npcols, reduce_exch_proc);
		
		double sum = 0.0;
		for (int i=0; i<s.n; i++) {
			sum = sum + s.a[i]*s.a[i];
		}
		res.val = allreduce(l2npcols, reduce_exch_proc, sum);
		moveout_single(result, res);
	}
}
