pragma options "-V20 --spmd-max-nproc 4 --bnd-inbits 3 --bnd-cbits 3 --bnd-unroll-amnt 27 --bnd-inline-amnt 3 --bnd-arr1d-size 27 --bnd-arr-size 54 --be:ufunSymmetry --be:nosim --beopt:simplifycex NOSIM --beopt:simiters 100 --be:lightverif"; //  --be:showDAG

int SPMD_MAX_NPROC = 4;
int SPMD_MAX_BUF_LEN = 36;
include "spmd.skh";

struct LState {
	int ntdivnp;		// total cells per node
	double[ntdivnp] a;
}


LState movein(global int nproc, int pid, global int nt, global double[nt] ga) {
	global int ntdivnp = nt/nproc;
	double[ntdivnp] a;
	int base = ntdivnp*pid;
	for (int i=0; i<ntdivnp; ++i) {
		a[i] = ga[base+i];
	}
	LState ls = new LState(ntdivnp=ntdivnp, a=a);
	return ls;
}

void moveout(global int nproc, int pid, global int nt, ref global double[nt] ga, LState ls) {
	int base = ls.ntdivnp*pid;
	for (int i=0; i<ls.ntdivnp; ++i) {
		ga[base+i] = ls.a[i];
	}
}

// logically the array is double[nx][ny][nz], i.e. nx is the most minor dimension.
// cells with same (y, z) but increasing x will occupy a contiguous region (of length nx) in memory
// we use a by-plane partition, so every node owns entire x and y axes, but portion of z axes
// after transpose, the new array result will have different dimensions: (x,y,z) => (z,x,y)
void spec(global int P, global int nx, global int ny, global int nz, global double[nx*ny*nz] a, ref global double[nx*ny*nz] result) {
	assume P>0 : "P>0";
	assume P<=SPMD_MAX_NPROC : "P<=MAX";
	//assume nx%P==0;
	assume ny%P==0 : "ny%P";
	assume nz%P==0 : "nz%P";

	assume nx>=1 && nx<=6 : "nx";
	assume ny>=1 && ny<=6 : "ny";
	assume nz>=1 && nz<=6 : "nz";
	assume nx*ny*nz <= 54 : "nx*ny*nz";
	assume nx*ny*nz/P <= 27 : "nx*ny*nz/P";
	
	// we don't use block now
    for(int y=0; y<ny; ++y) {
    		for (int x=0; x<nx; ++x) {
    			for (int z=0; z<nz; ++z) {
    				result[y*(nx*nz) + x*(nz) + z] = a[z*(nx*ny) + y*(nx) + x];
    			}
    		}
    }
}


// n = nx*ny*nz/np
// [nx][ny][nz/np] => [nz/np][nx][ny]
void transpose_local(global int n, global int nx, global int ny, global int nz, double[n] a, ref double[n] b) {
//	generator int num() {
//		return {| nx | ny | nz | 1 |} * {| ny | nz | 1 |} * {| nz | 1 |} / {| spmdnproc | 1 |};
//	}
//	int iend = num();
	for (int i=0; i<nz/spmdnproc; ++i) {
//	for (int i=0; i<iend; ++i) {
//		int jend = num();
		for (int j=0; j<nx*ny; ++j) {
//		for (int j=0; j<jend; ++j) {
			b[j*nz/spmdnproc + i] = a[i*nx*ny + j]; 
			//b[i*num() + j*num()] = a[i*num() + j*num()]; 
		}
	}
}

// n = nx*ny*nz/np
// [nz/np][nx][ny/np][np] => [nz][nx][ny/np]
void transpose_finish(global int n, global int nx, global int ny, global int nz, double[n] a, ref double[n] b) {
//	generator int num() {
//		return {| nx | ny | nz | 1 |} * {| ny | nz | 1 |} * {| nz | 1 |} / {| spmdnproc | spmdnproc*spmdnproc | 1 |};
//	}
	for (int p=0; p<spmdnproc; ++p) {
//		int iend = num();
		for (int i=0; i<nx*ny/spmdnproc; ++i) {
//		for (int i=0; i<iend; ++i) {
//			int jend = num();
			for (int j=0; j<nz/spmdnproc; ++j) {
//			for (int j=0; j<jend; ++j) {
				b[p*nz/spmdnproc + i*nz + j] = a[p*nx*ny*nz/(spmdnproc*spmdnproc) + i*nz/spmdnproc + j];
//				b[p*num() + i*num() + j*num()] = a[p*num() + i*num() + j*num()];
			}
		}
	}
}


// transpose a to b
void transpose_xy_z(global int nx, global int ny, global int nz, LState a, ref LState b) {
	global int n = nx*ny*nz/spmdnproc;
	transpose_local(n, nx, ny, nz, a.a, b.a);
	spmdalltoall(n, b.a, a.a);
	transpose_finish(n, nx, ny, nz, a.a, b.a);
}


void sk(global int P, global int nx, global int ny, global int nz, global double[nx*ny*nz] a, ref global double[nx*ny*nz] result) implements spec {
	global int nt = nx*ny*nz;
	spmdfork(P) {
		LState la;
		la = movein(P, spmdpid, nt, a);
		LState lr;
		lr = movein(P, spmdpid, nt, result);
		spmdbarrier();
		transpose_xy_z(nx, ny, nz, la, lr);
		spmdbarrier();
		moveout(P, spmdpid, nt, result, lr);
	}
}
