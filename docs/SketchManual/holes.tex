\section{Constant Generators and Specs}

Sketching extends a simple procedural language with the ability to leave \emph{holes} in place of code fragments that are to be derived by the synthesizer. Each hole is marked by a generator which defines the set of code fragments that can be used to fill a hole. \Sk{} offers a rich set of constructs to define generators, but all of these constructs can be described as syntactic sugar over a simple core language that contains only one kind of generator: an unknown integer constant denoted by the token \C{??}.

From the point of view of the programmer, the integer generator is a placeholder that the synthesizer must replace with a suitable integer constant. The synthesizer ensures that the resulting code will avoid any assertion failures under any input in the input space under consideration. For example, the following code snippet can be regarded as the ``Hello World'' of sketching.
\begin{lstlisting}
harness void main(int x){
   int y = x * ??;
   assert y == x + x;
}
\end{lstlisting}
This program illustrates the basic structure of a sketch. It contains three elements you are likely to find in every sketch: (i) a \C{harness} procedure, (ii) holes marked by generators, and (iii) assertions.

The harness procedure is the entry point of the sketch, and together with the assertion it serves as an operational specification for the desired program. The goal of the synthesizer is to derive an integer constant $C$ such that when \C{??} is replaced by $C$, the resulting program will satisfy the assertion for all inputs under consideration by the verifier. For the sketch above, the synthesized code will look like this. 
\begin{lstlisting}
void main(int x){
   int y = x * 2;
   assert y == x + x;
}
\end{lstlisting}


\subsection{Harnesses and function equivalence requirement}
A program in sketch can have multiple harness functions each encoding different requirements of the problem. Sketch will guarantee that all harnesses will run to completion without triggering assertion failures for all inputs within bounds. Harness functions are not allowed to take heap allocated objects as inputs and all global variables are reset to their initial values before the evaluation of each harness. Because of this, the order of evaluation of the harnesses does not matter. 

Sketch also allows the programmer to express that a function---we call it the implementation---must be functionally equivalent to another function---the specification---by writing \C{implements $fname$} at the end of the signature of the implementation function. When the specification and implementation do not access global variables, the equivalence constraint created by implements can be seen as syntactic sugar for a harness that calls both the implementation and the specification and compares their results. However, using \C{implements} is preferable to writing such a harness because it lets the compiler know that it can replace one function for the other when reasoning about the program. The implements directive imposes stronger constraints than harness; same function cannot have a \C{harness} and an \C{implements} directive.

If the implementation or the specification access global variables, then their equivalence must hold for all possible values of those global variables---not just for their initial values---and they must leave the global variables in the same state after they terminate. This is to ensure that replacing the specification with its implementation will always be semantics preserving. For this same reason, the spec and the sketch cannot access global variables that point to heap allocated values.

\begin{Example}
As a simple example of the use of harnesses and \C{implements}, consider the following sketch.
\begin{lstlisting}
int count=0;
int twox(int x, int y){
	++count;
	return x + x;
}

int expr1(int x, int y){
	return x*?? + y*??;
}
int expr2(int x, int y) implements twox{
	count = count+??;
	return x*?? + y*??;
}

harness void foo(){
	assert expr1(5,2)*expr2(2,4)== 24;
}
\end{lstlisting}
The harness imposes a constraint on the product of the two functions. The \C{implements} in the declaration of \C{expr2} imposes the additional constraint that \C{expr2} must be equivalent to \C{twox}. Because \C{twox} modifies the global variable \C{count}, \C{expr2} must modify the variable in the same way.

\end{Example}

\paragraph{Implementation note} As of the current \Sk{} release, there is a requirement that the specification should not have any unknowns. It is likely that this requirement will be relaxed in future releases. Also, when two functions are related by an \C{implements} directive, the compiler assumes that the specification is simpler than the implementation. The compiler exploits this by replacing every call to the implementation with a call to the specification during the analysis phase. This generally leads to big performance improvements for the solver, but it means that if the implementation calls itself recursively the system will not catch bugs that lead to infinite recursion. 


\subsection{Assumptions}
Starting in \Sk{} 1.6.7, the compiler supports standard \C{assume} statements. The semantics of \C{assume $cond$;} dictate that when the condition $cond$ is false, execution stops and any subsequent assertions are ignored. When a function is called by another function, any assumptions in the callee become assertions from the point of view of the caller; \ie{} the caller must guarantee that the inputs and environment it passes to the callee satisfy the assumptions. \emph{Note that this is not true of generator functions (\secref{generators})}; as we will describe later, generators get inlined into their calling context, so any \C{assume} statement in the generator is an assume statement in the function that invokes the generator.

Also, when a function implements a specification, its assumptions must be weaker than those of the specification. In other words, any input that is legal for the specification must be legal for the implementation. 

\begin{Example}{Assume and implements}
\begin{lstlisting}
harness int foo(int x){
	assume x > 10;
	int t= x-10;
	assert t > 0;
	return t;
}

int moo(int x) implements foo{
	assume x > 3;
	int t = x-??;
	assert t > 0;
	return t;
}


harness void main(int x){
	assume x > 5;
	int t = ??;
	moo(x+t);
	minimize(t);
}
\end{lstlisting}
In the example above, the harness \C{foo} assumes \C{x>10}, which allows it to satisfy the assertion \C{t>0}. In the case of \C{moo}, the unknown will resolve to \C{10} because of the constraint of equivalence with \C{foo}. Note that the assumption in \C{moo} is not strong enough to prove the assertion, but because \C{moo} implements \C{foo}, it inherits its preconditions. 
\end{Example}


\subsection{Types for Constant Generators}
The constant hole \C{??} can actually stand for any of the following different types of constants: 
\begin{itemize}
	\item Integers (\C{int})
	\item Booleans (\C{bit})
	\item Constant sized arrays and nested constant sized arrays
\end{itemize}

The system will use a simple form of type inference to determine the exact type of a given hole.

\subsection{Ranges for holes}
When searching for the value of a constant hole, the synthesizer will only search values greater than or equal to zero and less than $2^N$, where $N$ is a parameter given by the flag \C{--bnd-cbits}. If you wan to be explicit about the number of bits for a given hole, you can state it as \C{??(N)}, where \C{N} is an integer constant.

\flagdoc{bnd-cbits}{
The flag \C{bnd-cbits} tells the synthesizer what range of values to consider for all integer holes. If one wants a given integer hole to span a different range of values, one can use the extended notation \C{??(N)}, where \C{N} is the number of bits to use for that hole.
}


\subsection{Minimizing Hole Values}
In many cases, it is useful to ask the synthesizer for the smallest constant that will satisfy a specification. For such cases, the synthesizer supports a function \C{minimize($e$)}, which asks the synthesizer to make $e$ as small as possible. More specifically, the synthesizer will find a minimal $bnd$ such that $e < bnd$ for all inputs. If the program includes multiple \C{minimize} statements, the synthesizer will find a locally minimal set of bounds such that there is no other set of bounds that is strictly better than the one found. 

\flagdoc{bnd-mbits}{
The flag \C{bnd-mbits} tells the synthesizer how many bits to use to represent all bounds introduced by \C{minimize(e)}(default 5). Note that the largest value of \C{(e)} will be less than the bound, so if \C{e} can have value $n$, the bound needs enough bits to be able to reach $n+1$.
}


\begin{Example}
For example, consider the following simple program: 

\begin{lstlisting}
harness void main(int i, int j){
	int i_orig=i, j_orig=j;
	if(i > ??){ // we'll call this unknown u1
		i = ??; // u2
	}
	if(j > ??){ // u3
		j = ??; // u4
	}
	if(i_orig > 3 && j_orig > 3)
    	   assert 2*i + j > 6;
	minimize(i);
	minimize(j);
}
\end{lstlisting}
In the program above, synthesizer will try to minimize the upper bound on \C{i} (we'll call it $b_i$) and the upper bound on j ($b_j$). One possible solution is to have $u1=3$, $u2=4$, $u3=0$, $u4=0$. This will allow the upper bounds $(b_1, b_2)$ to be $(5,1)$. A different possible solution is to have $u1=0$, $u2=0$, $u3=0$, $u4=7$, which will allow the upper bounds to be $(1, 8)$. While the first set of bounds appears better than the second, they are actually incomparable. By contrast, a solution that had $(b_1, b_2)=(6,1)$ would not be allowed because the solution with bounds $(5,1)$ is strictly better.
\end{Example}



The synthesizer has some syntactic sugar on top of \C{minimize} to support the synthesis of a minimal number of statements, a common idiom in \Sk{}. For example, the code below will produce the minimal number of assignments required to swap \C{x} and \C{y}.
\begin{lstlisting}
void swap(ref bit[W] x, ref bit[W] y){
    minrepeat{
        if(??){ x = x ^ y;}else{ y = x ^ y; }
    }
}

harness void main(bit[W] x, bit[W] y){
    bit[W] tx = x; bit[W] ty = y;
    swap(x, y);
    assert x==ty && y == tx;
}
\end{lstlisting}


\section{Generator functions}
\seclabel{generators}
A generator describes a space of possible code fragments that can be used to fill a hole. The constant generator we have seen so far corresponds to the simplest such space of code fragments: the space of integers in a particular range. More complex generators can be created by composing simple generators into \emph{generator functions}. 

As a simple example, consider the problem of specifying the set of linear functions of two parameters \C{x} and \C{y}. That space of functions can be described with the following simple generator function:
\begin{lstlisting}
generator int legen(int i, int j){
   return ??*i + ??*j+??;
}
\end{lstlisting}

The generator function can be used anywhere in the code in the same way a function would, but the semantics of generators are different from functions. In particular, every call to the generator will be replaced by a concrete piece of code in the space of code fragments defined by the generator. Different calls to the generator function can produce different code fragments. For example, consider the following use of the generator.


\begin{lstlisting}
harness void main(int x, int y){

  assert legen(x, y) == 2*x + 3;
  assert legen(x,y) == 3*x + 2*y;

}
\end{lstlisting}


Calling the solver on the above code produces the following output
\begin{lstlisting}
void _main (int x, int y){
  assert ((((2 * x) + (0 * y)) + 3) == ((2 * x) + 3));
  assert (((3 * x) + (2 * y)) == ((3 * x) + (2 * y)));
}
\end{lstlisting}

Note that each invocation of the generator function was replaced by a concrete code fragment in the space of code fragments defined by the generator. 

The behavior of generator functions is very different from standard functions. If a standard function has generators inside it, those generators are resolved to produce code that will behave correctly in all the calling contexts of the function as illustrated by the example below. 
\begin{lstlisting}
int linexp(int x, int y){
   return ??*x + ??*y + ??;
}
harness void main(int x, int y){
   assert linexp(x,y) >= 2*x + y;
   assert linexp(x,y) <= 2*x + y+2;
}
\end{lstlisting}
For the routines above, there are many different solutions for the holes in \C{linexp} that will satisfy the first assertion, and there are many that will satisfy the second assertion, but the synthesizer will chose one of the candidates that satisfy them both and produce the code shown below. Note that the compiler always replaces return values for reference parameters, but other than that, the code below is what you would expect.
\begin{lstlisting}
void linexp (int x, int y, ref int _out){
  _out = 0;
  _out = (2 * x) + (1 * y);
  return;
}
void _main (int x, int y){
  int _out = 0;
  linexp(x, y, _out);
  assert (_out >= ((2 * x) + y));
  int _out_0 = 0;
  linexp(x, y, _out_0);
  assert (_out_0 <= (((2 * x) + y) + 2));
}
\end{lstlisting}



\subsection{Recursive Generator Functions}

Generators derive much of their expressive power from their ability to recursively define a space of expressions. For example, the code below shows how to use a recursive generator to define a context free grammar of possible expressions.

\begin{Example}{Recursive Generator}
\begin{lstlisting}
generator int rec(int x, int y, int z){
   int t = ??;
   if(t == 0){return x;}   
   if(t == 1){return y;}
   if(t == 2){return z;}

   int a = rec(x,y,z);
   int b = rec(x,y,z);

   if(t == 3){return a * b;}
   if(t == 4){return a + b;} 
   if(t == 5){return a - b;}   
}
harness void sketch( int x, int y, int z ){
   assert rec(x,y, z) == (x + x) * (y - z);
}
\end{lstlisting}
\end{Example}

One must be careful when defining recursive generators, however, because the way the generator is defined can have a dramatic impact on the solution time of the resulting code. In particular, there are two aspects that the writer must keep in mind when writing a generator: recursion and symmetries. 


\paragraph{Recursion control in generators}
The compiler handles recursive generators by inlining them number of times as guided by the \C{bnd-inline-amnt} flag. This simple approach can cause problems if recursive generators are not written carefully. For example, an alternative way of writing the generator above is shown below.  

\begin{Example}{Inefficient recursive generator}
\begin{lstlisting}
generator int rec(int x, int y, int z){
   int t = ??;
   if(t == 0){return x;}   
   if(t == 1){return y;}
   if(t == 2){return z;}
   if(t == 3){return rec(x,y,z) * rec(x,y,z);}
   if(t == 4){return rec(x,y,z) + rec(x,y,z);} 
   if(t == 5){return rec(x,y,z) - rec(x,y,z);}   
}
harness void sketch( int x, int y, int z ){
   assert rec(x,y, z) == (x + x) * (y - z);
}
\end{lstlisting}
\end{Example}
Both generators describe the same grammar, and therefore in principle the same space of possible expressions, but the second generator will cause problems because each recursive call to \C{rec} will be inlined independently, so each level of inlining will increase the size of the program by a factor of six, instead of only a factor of two. 

Another potential issue with recursive generators is that the amount of inlining is controlled by the same flag used to control inlining of functions during analysis. This can be problematic because recursive functions in the program will often require much more inlining than generators. To address this problem, the user can take additional control over inlining by explicitly adding a bound parameter into the generator as shown below. 

\begin{Example}{Generator with manual inlining control}
\begin{lstlisting}
generator int rec(int x, int y, int z, int bnd){
   assert bnd >= 0;
   int t = ??;
   if(t == 0){return x;}   
   if(t == 1){return y;}
   if(t == 2){return z;}

   int a = rec(x,y,z, bnd-1);
   int b = rec(x,y,z, bnd-1);
   ...
}
\end{lstlisting}
\end{Example}

The synthesizer performs partial evaluation in tandem with inlining, so if we call rec with a constant value for the \C{bnd} parameter, the synthesizer will stop inlining when it determines that this parameter will be less than zero. 

\paragraph{Avoiding symmetries} 
Another aspect to be careful with when defining recursive generators are symmetries. These happen when different assignments to unknown values can result in the exact same expression. An important source of symmetries are commutative and associative operations. For example, consider two generators shown below. 

\begin{Example}{Effect of symmetries on generators}
\begin{lstlisting}
generator int sum(int x, int y, int z, int bnd){
    assert bnd > 0;
   generator int factor(){
       return {| x | y | z|} * {| x | y | z | ?? |};
   }
   if(??){ return factor(); }
   else{return sum(x,y,z, bnd-1) + sum(x,y,z, bnd-1);} 
}

generator int sumB(int x, int y, int z, int bnd){
    assert bnd > 0;
    generator int factor(){
        return {| x | y | z|} * {| x | y | z | ?? |};
    }
    if(??){ return factor(); }
    else{ return factor() + sumB(x,y,z, bnd-1);} 
 }
\end{lstlisting}
\end{Example}

Both represent the same space of expressions, but the generator \C{sumB} forces a right-associativity on the expression, whereas the generator \C{sum} can produce all possible associations, making the generator \C{sumB} more efficient than \C{sum}. Additionally, in \C{sumB} the \C{bnd} parameter has a clear meaning: it is the number of terms in the sum, whereas in generator \C{sum}, the parameter \C{bnd} is the depth of the AST, which is not as straightforward to map to something meaningful to the programmer. 


\subsection{Regular Expression Generators}

Sketch provides some shorthand to make it easy to express simple sets of expressions. This shorthand is based on regular expressions. Regular expression generators describe to the synthesizer a set of choices from which to choose in searching for a correct solution to the sketch. The basic syntax is
\begin{lstlisting}
{| regexp |}
\end{lstlisting}

Where the regexp can use the operator | to describe choices, and the operator ? to define optional subexpressions.

For example, the sketch from the previous subsections can be made more succinct by using the regular expression shorthand. 

\begin{lstlisting}
generator int rec(int x, int y, int z){ 
    if(??){ 
        return {| x | y | z |};
    }else{
        return {| rec(x,y,z) (+ | - | *) rec(x,y,z)  |};
    }
}

harness void sketch( int x, int y, int z ){
   assert rec(x,y, z) == (x + x) * (y - z);
}
\end{lstlisting}


Regular expression holes can also be used with pointer expressions. For example, suppose you want to create a method to push a value into a stack, represented as a linked list. You could sketch the method with the following code:

\begin{lstlisting}
push(Stack s, int val){
  Node n = new Node();
  n.val = val;
  {|  (s.head | n)(.next)? |} =   {|  (s.head | n)(.next)? |};
  {|  (s.head | n)(.next)? |} =   {|  (s.head | n)(.next)? |};
}
\end{lstlisting}





\subsection{Local Variables Construct}\seclabel{localvariablesconstruct}

Sketch supports the use of the \C{$\$$(type)} construct to instruct the synthesizer to consider all variables of the specified \C{type} within scope when searching for a solution. 

\begin{lstlisting}
harness void main(int x) {
    int a = 2;
    double b = 2.3;
        
    assert x * $\$$(int) == x + x; $//$ $\$$(int) === {| 0  |  a  |  x |}
}
\end{lstlisting}

The value of \textit{type} can be any of the primitive types (see \secref{primitives}) or any user defined type. The default value of any primitive type will also be considered as one of the choices. Local variables inside a function and its formal parameters are considered within scope of the construct. If the construct is used inside a local function, the local variables and formal parameters of the functions where it is defined are also within scope of the construct.


\subsection{High order generators}\seclabel{high-ordergenerators}

Generators can take other generators as parameters, and they can be passed as parameters to either generators or functions. This can be very useful in defining very flexible classes of generators. For example, the generator rec above assumes that you want expressions involving three integer variables, but in some cases you may only want two variables, or you may want five variables. The following code describes a more flexible generator: 

\begin{lstlisting}
generator int rec(fun choices){ 
    if(??){ 
        return choices();
    }else{
        return {| rec(choices) (+ | - | *) rec(choices)  |};
    }
}
\end{lstlisting}

We can use this generator in the context of the previous example as follows:
\begin{lstlisting}
harness void sketch( int x, int y, int z ){
   generator int F(){
	return {| x | y | z |};
   }
   assert rec(F) == (x + x) * (y - z);
}
\end{lstlisting}

In a different context, we may want an expression involving some very specific sub-expressions, but the same generator can be reused in the new context.
\begin{lstlisting}
harness void sketch( int N, int[N] A, int x, int y ){   
   generator int F(){
	return {| A[x] | x | y |};
   }
   if(x<N){
	   assert rec(F) == (A[x]+y)*x;
   }
}
\end{lstlisting}

High order generators can also be used to describe patterns in the expected structure of the desired code. For example, if we believe the resulting code will have a repeating structure, we can express this with the following high-order generator: 

\begin{lstlisting}
generator void rep(int n, fun f){
    if(n>0){
        f();
        rep(n-1, f);
    }    
}
\end{lstlisting}



\subsection{\C{repeat} construct}
The pattern illustrated by the \C{rep} generator above---where the user wants to generate multiple statements, all of which can be generated from the same code with holes---is quite common. Sketch includes a repeat construct to do exactly this: 
\begin{lstlisting}
repeat($N$){
     $stmt$
}
\end{lstlisting}
This construct is just syntactic sugar for the recursive $rep$ generator shown earlier. A big difference between using \C{repeat} and writing your own \C{rep} generator is that with repeat, the maximum number of repetitions will be dictated by the loop unrolling bound \C{bnd-unroll-amnt}, whereas if you write your own recursive generator, the maximum number of repetitions will be dictated by the inlining bound \C{bnd-inline-amnt}. Also note that if $N$ is not a constant or a constant hole, the result of using this construct will be a set of nested if statements.

There is also a convenient variant of this construct: 
\begin{lstlisting}
repeat($i$ : $N$){
     $stmt$
}
\end{lstlisting}
Where $i$ is a fresh variable name, which will automatically be declared as an integer and can be used within $stmt$ to keep track of which copy you are in. The variable will have the value $n$ in the $n^{th}$ copy of $stmt$. As an example of how this may be used, consider the following code:


\begin{lstlisting}

  generator int add([int n, int k], int[n] A, int idx, int[k] offst){
    int res = 0;
    repeat(i: k){ 
         res += A[idx + offst[i]];
    }
    return res;
  }

  int[n] combine([int n], int[n] A){
         int[n] B;
         for(int i=1; i<n-1; ++i){
              int[3] offsts = {??-1, ??-1, ??-1}; //The -1 are necessary because ?? can only take positive values.
               B[i] = add(A, i, offsts);
         }
         return B;
  }

  harness void main(){
       assert combine({2, 4, 5}) == {0, 11, 0};
  }

\end{lstlisting}

Note that in the sketch above, the \C{-1} in the definition of offsts is necessary because constant holes \C{??} can only take non-negative values.
Given the harness, the combine function above will be synthed into the code below.

\begin{lstlisting}

void combine (int n, int[n] A, ref int[n] _out)
{
  _out = ((int[n])0);
  for(int i = 1; i < (n - 1); i = i + 1)
  {
    int _out_s3 = A[i];
    _out_s3 = _out_s3 + (A[i + -1]);
    _out_s3 = _out_s3 + (A[i + 1]);
    _out[i] = _out_s3;
  }
  return;
}

\end{lstlisting}

Note how the repeat is expanded into three separate statements that read from \C{A} and add the result to the output variable. The compiler is able to tell that the first one is adding to zero, so it simplifies it and folds it into the declaration of the variable. 


