// a thorough test of angelic feature: the model based expr generator, which might be faster
// this triggers a bug in the backend's miniSAT solver.cpp: see backend Issue #6

pragma options "--be:nosim --bnd-inbits 4 --bnd-cbits 3 --bnd-angelicbits 4 --bnd-unroll-amnt 6 --bnd-inline-amnt 2 --beopt:simplifycex NOSIM";

include "generators.skh";

package generators;
int maxlen = 5;
int maxint = 15;

int[maxlen+1] moo(int len, int N, int[N] in, int[maxlen] rator, int[maxlen] xrand, int[maxlen] yrand);

model int[maxlen+1] calc(int len, int N, int[N] in, int[maxlen] rator, int[maxlen] xrand, int[maxlen] yrand) {
	int[maxlen+1] result = moo(len, N, in, rator, xrand, yrand);
	
	generator int access(int p) {
		int v;
		if (p < N) {
			v = in[p];
		} else {
			v = result[p-N];
		}
		return v;
	}
	
	bit flag = 0;
	int i = 0;
	repeat(maxlen) {
		int o = rator[i];
		int x = xrand[i];
		int y = yrand[i];

		if (! (o<=MOD && x<N+i && y<N+i) ) {
			flag = 1;
		}
		
		int a = access(x);
		int b = access(y);
		if (o == PLUS) {
			int r = a+b;
			if (r > maxint) {
				flag = 1;
			} else {
				assert r == result[i];
			}
		}
		if (o == MINUS) {
			int r = a-b;
			if (a < b) {
				flag = 1;
			} else {
				assert r == result[i];
			}
		}
		if (o == TIMES) {
			int r = a*b;
			if (r > maxint) {
				flag = 1;
			} else {
				assert r == result[i];
			}
		}
		if (o == DIV) {
			int r = a/b;
			if (b == 0) {
				flag = 1;
			} else {
				assert r == result[i];
			}
		}
		if (o == MOD) {
			int r = a%b;
			if (b == 0) {
				flag = 1;
			} else {
				assert r == result[i];
			}
		}
		
		i++;
	}
	
	assert flag == result[maxlen];
	return result;
}

/**
 * 
 * Generate an expression based on the N operands and T operators
 * given.
 * 
 */
generator int expr3(int N, int[N] pars, int T, int[T] ops, int len) {
//	int[maxlen] rator = {3, 0, 2};
//	int[maxlen] xrand = {2, 4, 0};
//	int[maxlen] yrand = {1, 3, 5};
	int[maxlen] rator;
	int[maxlen] xrand;
	int[maxlen] yrand;
	int i = 0;
	repeat(maxlen) {
		int r = ops[??];
		rator[i] = r;
		
		int x = ??;
		assert x < N+i;
		xrand[i] = x;
		
		int y = ??;
		assert y < N+i;
		yrand[i] = y;
		
		i++;
	}
	
	int[maxlen+1] result = calc(len, N, pars, rator, xrand, yrand);
	assert result[maxlen] == 0;
	
	return result[len-1];
}

harness void partition(int p, int P, int N){
	assume P>0;
	assume N>0;
	assume p<P;
	assume p<8 && P < 8 && N < 8;
		
	generator int num() {
		int len = ??;
		assert len <= maxlen;
		minimize(len);
		return expr3(3, {p, P, N}, 4, {PLUS, TIMES, DIV, MOD}, len);
	}

	assert num() == N % P;
	assert num() == p * (1 + (N / P));
	assert num() == (1 + p) * (1 + (N / P));
	assert num() == p * (N/P) + N%P;
}
